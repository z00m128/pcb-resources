	LIST
;***** Makra z MDV74_1 pre PIC74
	NOLIST
;
; Porovnanie F s cislom N (rozlisi aj < >)
; F=N   Z,C
; F<N	NZ,NC
; F>N	NZ,C
;
cpi	macro	F,N
	movlw	N
	subwf	F,w
	endm
;
; Porovnanie F s registrom R (rozlisi aj < >)
; F=N   Z,C
; F<N	NZ,NC
; F>N	NZ,C
;
cp	macro	F,R
	movf	R,w
	subwf	F,w
	endm

; Skok na adr. A ak Z
;
jz	macro	A
	btfsc	STATUS,Z	;Ak Z=0 preskok
	goto	A		;Inak skok na adr. A
	endm
;
; Skok na adr. A ak NZ
;
jnz	macro	A
	btfss	STATUS,Z	;Ak Z=1 preskok
	goto	A		;Inak skok na adr. A
	endm
;
; Skok na adr. A ak C
;
jc	macro	A
	btfsc	STATUS,C	;Ak C=0 preskok
	goto	A		;Inak skok na adr. A
	endm
;
; Skok na adr. A ak NC
;
jnc	macro	A
	btfss	STATUS,C	;Ak C=1 preskok
	goto	A		;Inak skok na adr. A
	endm
;
; Skok na adr. A ak DC
;
jdc	macro	A
	btfsc	STATUS,DC	;Ak C=0 preskok
	goto	A		;Inak skok na adr. A
	endm
;
; Skok na adr. A ak NDC
;
jndc	macro	A
	btfss	STATUS,DC	;Ak C=1 preskok
	goto	A		;Inak skok na adr. A
	endm
;
; Naplnenie registra F hodnotou A
;
movlf	macro	F,A
	movlw	A
	movwf	F
	endm
;
; Naplnenie registra F hodnotou registra N
;
mov	macro	F,N
	movf	N,w
	movwf	F
	endm
;
; Vzajomna vymena registrov
;
ex	macro	F,N
	movf	F,w
	xorwf	N,w
	xorwf	F,f
	xorwf	N,f
	endm
;
; Preskok ak Z
;
sz	macro
	btfss	STATUS,Z
	endm
;
; Preskok ak NZ
;
snz	macro
	btfsc	STATUS,Z
	endm
;
; Preskok ak C
;
sc	macro
	btfss	STATUS,C
	endm
;
; Preskok ak NC
;
snc	macro
	btfsc	STATUS,C
	endm
;
; Preskok ak DC
;
sdc	macro
	btfss	STATUS,DC
	endm
;
; Preskok ak NDC
;
sndc	macro
	btfsc	STATUS,DC
	endm
;
;*****
;
; Skoc ak je bit FILE.B=1 na adresu ADR
;
jb	macro	FILE,B,ADR
	btfsc	FILE,B
	goto 	ADR
	endm
;
;*****
;
; Skoc ak je bit FILE.B=0 na adresu ADR
;
jnb	macro	FILE,B,ADR
	btfss	FILE,B
	goto 	ADR
	endm
;
;*****
;
; Odcitaj 1 od F a ak nie je = 0 skok
;
djnz	macro	F,ADR
	decfsz	F,f
	goto	ADR
	endm
;
;*****
;
; Nastav BANKU registrov 0 (RP1 je 0)
;
bank_0	macro
	bcf	STATUS,RP0
	endm
;
; Nastav BANKU registrov 1
;
bank_1	macro	
	bsf	STATUS,RP0
	endm
;	
;*****
;
; Nastav PAGE0 (PCLATH,3 je 0)
;
page_0	macro
	bcf	PCLATH,3
	endm
;
; Nastav PAGE1 (PCLATH,3 je 1)
;
page_1	macro	
	bsf	PCLATH,3
	endm
;	
;*****
;
; Ulozenie stavu pri INTERUPTE. WBUF a STBUF su odkladacie registre
; a musia byt v BANK0
;
push	macro	WBUF,STBUF

	movwf	WBUF
	swapf	STATUS,w
	bcf	STATUS,RP0	;Prepni BANKU 0 !
	movwf	STBUF
	
	endm		
;	
;*****
;
; Obnovenie stavu po INTERUPTE, musi byt nastavena BANK0 !!!
;
pop	macro	WBUF,STBUF

	swapf	STBUF,w
	movwf	STATUS
	swapf	WBUF,f
	swapf	WBUF,w
	
	endm		
;	
;*****
;
; Blokovanie INT, nestaci len BCF GIE!!!
;
disint	macro
	local loop

loop	bcf	INTCON,GIE
	btfsc	INTCON,GIE
	goto	loop

	endm
;
;*****
;
; Povolenie INT
;
enint	macro

	bsf	INTCON,GIE

	endm
;
;*****
;
	LIST		

